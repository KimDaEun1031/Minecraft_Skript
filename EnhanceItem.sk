#!==========================================!#
#!                                          !#
#!              아이템 강화                  !#
#!                                          !#
#!==========================================!#
# 제작: 포근한 블로그 새벽의 아이디어 https://zynar.tistory.com/
# 본 스크립트는 다운로드, 수정, 공유, 배포 등등 자유롭게 이용하셔도 됩니다.
# ver 1.3

options:
	s1: &f[&c!&f] ::&f
	s2:&f&l[!&7&lSYSTEM&f&l!] &f::&r



#!==========================================!#
#!                                          !#
#!              npc 클릭                    !#
#!                                          !#
#!==========================================!#

on rightclick on entity:
    # 무기 대장장이 오른쪽 클릭
    if entity's name is "무기대장장이":
        message "[무기대장장이]: 여! 무슨일이야?" to player
        wait 1 seconds
        message "[무기대장장이]: 미안하지만 지금은 강화밖에 할 수 없어." to player
        wait 1 seconds
        open chest with 5 rows named "&8&l무기 대장장이" to player
        set slot 0 and 2 and 4 and 6 and 8 and 18 and 26 and 36 and 38 and 40 and 42 and 44 of current inventory of player to Gray Wool named " " with lore " "
        set slot 1 and 3 and 5 and 7 and 9 and 17 and 27 and 35 and 37 and 39 and 41 and 43 of current inventory of player to Light Gray wool named " " with lore " " 
        set slot 20 of current inventory of player to Anvil named "&2무기 변형" with lore "&e왼쪽 클릭시 무기 변형 창을 엽니다." and "&e일반 무기를 먼저 변형해야만 강화를 할 수 있습니다."
        set slot 21 of current inventory of player to Gold Ingot named "&9무기 강화" with lore "&e왼쪽 클릭시 무기 강화 창을 엽니다."
        set slot 22 of current inventory of player to Diamond named "&c무기 제련" with lore "&e왼쪽 클릭시 무기 제련 창을 엽니다."
        set slot 23 of current inventory of player to Lapis Lazuli named "&a인첸트 강화" with lore "&e왼쪽 클릭시 인첸트 강화 창을 엽니다."
        set slot 24 of current inventory of player to Birch Sign named "&e말걸기" with lore "&e왼쪽 클릭시 npc와 대화"
        set slot 10 and 11 and 12 and 13 and 14 and 15 and 16 and 19 and 25 and 28 and 29 and 30 and 31 and 32 and 33 and 34 of current inventory of player to White Stained Glass Pane named " " with lore " "
        
#!==========================================!#
#!                                          !#
#!              강화 창                      !#
#!                                          !#
#!==========================================!#

on inventory click:
    if iname contains "&8&l무기 대장장이":
        if click type is left mouse button with shift or right mouse button with shift or right mouse button:
            cancel event
        else:
            if clicked raw slot is not 20 or 21 or 22 or 23 or 24:
                if clicked raw slot <= 44:
                    cancel event

            else if clicked raw slot is 20:
                cancel event
                open chest with 3 rows named "&2&l무기 변형" to player
                set slot 0 and 2 and 4 and 6 and 8 and 18 and 20 and 22 and 24 and 26 of current inventory of player to Gray wool named " " with lore " "
                set slot 1 and 3 and 5 and 7 and 9 and 17 and 19 and 21 and 23 and 25 of current inventory of player to Light Gray wool named " " with lore " "
                set slot 10 and 12 of current inventory of player to Stick named "&2도움말" with lore "&e무기를 넣는 곳"
                set slot 14 and 16 of current inventory of player to Stick named "&2도움말" with lore "&e무기가 나오는 곳"
                set slot 13 of current inventory of player to Anvil named "&c무기 변형" with lore "&e왼쪽 빈칸에 무기를 넣고" and "&e무기 변형 버튼을 누르면" and "&e오른쪽에 변형된 무기가 나옵니다." and "&c&l주의! 이미 변형된 무기를" and "&c&l변형하게 되면 0단계로 초기화 됩니다." and "&e이용 대금 5000"
                exit

            else if clicked raw slot is 21:
                cancel event
                open chest with 5 rows named "&9&l무기 강화" to player
                loop 45 times:
                    if loop-value - 1 is 1 or 3 or 5 or 7 or 9 or 17 or 27 or 35 or 37 or 39 or 41 or 43:
                        format slot loop-value - 1 of player with Light Gray Wool named " " with lore " " to be unstealable
                    
                    else if loop-value - 1 is 0 or 2 or 4 or 6 or 7 or 8 or 18 or 26 or 36 or 38 or 40 or 42 or 44:
                        format slot loop-value - 1 of player with Gray Wool named " " with lore " " to be unstealable
                    
                set slot 10 and 28 of current inventory of player to Birch Sign named "&2도움말" with lore "&e빈칸에 강화를 하고자 하는 무기를 둡니다."
                set slot 12 of current inventory of player to Book named "&2도움말" with lore "&e아래에 현재 무기보다 1단계 높은 강화석을 둡니다."
                set slot 13 of current inventory of player to Book named "&2도움말" with lore "&e아래에 행운석을 둡니다." and "&e행운석은 강화 확률을 올려주며 단계 하락을 방지합니다."
                set slot 14 of current inventory of player to Book named "&2도움말" with lore "&e아래에 파괴 방지 부적을 둡니다." and "&e파괴 방지 부적은 파괴를 방지해줍니다."
                set slot 22 of current inventory of player to Anvil named "&4무기 강화" with lore "&e왼쪽 클릭시 일정확률로 강화에 성공합니다." and "&l실패시 단계 하락 및 사용불가될 수 있습니다." and "&e강화 확률과 대금은 </강화 확률>을 통해 볼 수 있습니다."
                set slot 16 and 34 of current inventory of player to Birch Sign named "&2도움말" with lore "&e강화 성공시 빈칸에 강화된 아이템이 나옵니다."
                
                set slot 11 and 19 and  21 and 29 and 15 and 23 and 25 and 33 of current inventory of player to Red Wool named " " with lore " "
                exit 

            else if clicked raw slot is 22:
                cancel event
                open chest with 5 rows named "&c&l무기 제련" to player
                loop 45 times:
                    if loop-value - 1 is 1 or 3 or 5 or 7 or 9 or 17 or 27 or 35 or 37 or 39 or 41 or 43:
                        format slot loop-value - 1 of player with Light Gray Wool named " " with lore " " to be unstealable
                    
                    else if loop-value - 1 is 0 or 2 or 4 or 6 or 7 or 8 or 18 or 26 or 36 or 38 or 40 or 42 or 44:
                        format slot loop-value - 1 of player with Gray Wool named " " with lore " " to be unstealable
                    else if loop-value - 1 is 11 or 19 or 21 or 29:
                        format slot loop-value - 1 of player with Orange Wool named " " with lore "&e중앙 빈칸에 아이템을 올려놓으세요." to be unstealable
                    else if loop-value - 1 is 22:
                        set slot loop-value - 1 of current inventory of player to Anvil named "&c무기 제련" with lore "&e왼쪽에 아이템, 오른쪽에 제련석을 두고 누르면" and "&e제련석 효과가 아이템에 전해집니다." and "&e대금: 350000"
                    else if loop-value - 1 is 15 or 23 or 25 or 33:
                        set slot loop-value - 1 of current inventory of player to Yellow Wool named " " with lore "&e중앙 빈칸에 제련석을 두세요."
                    else if loop-value - 1 is 12 or 13 or 14 or 30 or 31 or 32:
                        set slot loop-value - 1 of current inventory of player to White Stained Glass Pane named " " with lore " "
                    else if loop-value - 1 is 10 or 16 or 28 or 34:
                        set slot loop-value - 1 of current inventory of player to Birch Sign named " " with lore " "
                exit

            else if clicked raw slot is 23:
                cancel event
                open chest with 5 rows named "&b&l인첸트 강화" to player
                loop 45 times:
                    if loop-value - 1 is 1 or 3 or 5 or 7 or 9 or 17 or 27 or 35 or 37 or 39 or 41 or 43:
                        format slot loop-value - 1 of player with Light Gray Wool named " " with lore " " to be unstealable
                    
                    else if loop-value - 1 is 0 or 2 or 4 or 6 or 7 or 8 or 18 or 26 or 36 or 38 or 40 or 42 or 44:
                        format slot loop-value - 1 of player with Gray Wool named " " with lore " " to be unstealable
                    else if loop-value - 1 is 11 or 19 or 21 or 29:
                        format slot loop-value - 1 of player with Blue Wool named " " with lore "&e중앙 빈칸에 아이템을 올려놓으세요." to be unstealable
                    else if loop-value - 1 is 22:
                        set slot loop-value - 1 of current inventory of player to Anvil named "&e인첸트 강화" with lore "&e왼쪽에 아이템, 오른쪽에 인첸트 강화 부적을 두고 누르면" and "&e인첸트 강화 부적의 효과가 무기에 전해집니다." and "&e대금: 50000"
                    else if loop-value - 1 is 15 or 23 or 25 or 33:
                        set slot loop-value - 1 of current inventory of player to Light Blue Wool named " " with lore "&e중앙 빈칸에 인첸트 강화 부적을 두세요."
                    else if loop-value - 1 is 12 or 13 or 14 or 30 or 31 or 32:
                        set slot loop-value - 1 of current inventory of player to Stick named " " with lore " "
                    else if loop-value - 1 is 10 or 16 or 28 or 34:
                        set slot loop-value - 1 of current inventory of player to White Stained Glass Pane named " " with lore " "
                exit

            else if clicked raw slot is 24:
                cancel event
                message "[무기대장장이]: 음? 할일이 없는거냐?" to player
                exit

    # 무기 제련 창
    else if iname contains "&c&l무기 제련":
        if click type is left mouse button with shift or right mouse button with shift or right mouse button:
            cancel event
        else:
            if clicked raw slot is not 20 or 22 or 24:
                clicked raw slot <= 44:
                    cancel event
                    exit
            
            else if clicked raw slot is 22:
                cancel event
                message "미안 아직 할 수 없어" to player
                exit

    else if iname contains "&b&l인첸트 강화":
        if click type is left mouse button with shift or right mouse button with shift or right mouse button:
            cancel event
        else:
            if clicked raw slot is not 20 or 22 or 24:
                clicked raw slot <= 44:
                    cancel event
                    exit
            
            else if clicked raw slot is 22:
                cancel event
                set {_pitem} to slot 20 of current inventory of player
                set {_encitem} to slot 24 of current inventory of player
                set {_enclore::*} to uncolored lore of {_encitem}
                set {_am} to amount of {_encitem} in current inventory of player
                replace all " " and "+" and "1" with "" in {_enclore::1}

                if {_pitem} or {_encitem} is air:
                    message "[무기대장장이]: 어이! 무기와 인챈트 강화석 확실하게 올려두라고!" to player
                    exit

                else if {_enclore::2} is not "인챈트":
                    message "[무기대장장이]: 음, 이건 인챈트 강화석이 아닌데? 다른걸 가져와" to player
                    exit
                
                else:
                    set {_enc} to transEnchant({_enclore::1}) parsed as enchantment
                    set {_pitemenlv} to enchant level of {_enc} of {_pitem}
                    set {_isMax} to isMaxEnchant({_enclore::1}, {_pitemenlv}, player)

                    if {_isMax} is false:
                        delete slot 20 of current inventory of player
                        delete slot 24 of current inventory of player
                        wait 12 tick
                        set {_amount} to {_am} - 1
                        set slot 24 of current inventory of player to {_amount} of {_encitem}
                        enchant {_pitem} with "%{_enc}% %{_pitemenlv} + 1%" parsed as enchantment type
                        set slot 20 of current inventory of player to {_pitem}
                        subtract 50000 from player's money
                        message "[무기대장장이]: 아이템의 인챈트가 강화되었다. 잘쓰라고!" to player
                        play sound "BLOCK_ANVIL_USE" with volume 1 and pitch 1 at player
                        launch flickering ball colored red and yellow and green at player's location timed 0.8
                        exit
                    
                    else:
                        message "[무기대장장이]: 음, 이 무기의 %{_enclore::1}% 인챈트는 더이상 올릴 수 없어." to player
                        exit

 

    # 무기 강화 창
    else if iname contains "&9&l무기 강화":
        if click type is left mouse button with shift or right mouse button with shift or right mouse button:
            cancel event
        else:
            if clicked raw slot is not 20 or 22 or 24 or 30 or 31 or 32:
                clicked raw slot <= 44:
                    cancel event
                    exit

            else if clicked raw slot is 22:
                cancel event
                # 해당 변수에 slot 20에 있는 아이템의 lore을 색깔 빼고 입력
                set {_아이템} to slot 20 of current inventory of player
                set {_장비스탯::*} to uncolored lore of slot 20 of current inventory of player
                replace all "단계" with "" in {_장비스탯::2}
                replace all " " and "+" and "체력" with "" in {_장비스탯::4}
                replace all " " and "+" and "공격력" with "" in {_장비스탯::5}
                replace all " " and "+" and "방어력" with "" in {_장비스탯::6}
                set {_강화단계} to {_장비스탯::2} parsed as number
                set {_강화체력} to {_장비스탯::4} parsed as number
                set {_강화공격력} to {_장비스탯::5} parsed as number
                set {_강화방어력} to {_장비스탯::6} parsed as number
                
                # 강화석 정보를 변수에 대입
                set {_강화석스탯::*} to uncolored lore of slot 30 of current inventory of player
                replace all " " and "단계" and "아이템을" and "강화하는데" and "쓰입니다." with "" in {_강화석스탯::1}
                set {_강화석스탯} to {_강화석스탯::1} parsed as number

                # 행운석 정보를 변수에 대입
                set {_행운석스탯::*} to uncolored lore of slot 31 of current inventory of player
                replace all " " and "강화" and "확률을" and "%%" and "올려줍니다." with "" in {_행운석스탯::1}
                set {_행운석스탯} to {_행운석스탯::1} parsed as number
                set {_luckitem} to slot 31 of current inventory of player

                # 주문서 정보를 변수에 대입
                set {_강화주문서::*} to uncolored lore of slot 32 of current inventory of player
                # 주문서 자체가 있으면 되니까 {_강화주문서::2}만 있으면 됨
                set {_강화주문서} to slot 32 of current inventory of player

                if {_luckitem} is not air:
                    if {_행운석스탯::3} is not "행운석":
                        message "[무기대장장이]: 행운석 자리에 이상한걸 넣었군" to player
                        exit
                    else:
                        set {_is행운석} to true
                else:
                    set {_is행운석} to false

                if {_강화주문서} is not air:
                    if {_강화주문서::2} is not "주문서":
                        message "[무기대장장이]: 강화 주문서 자리에 이상한걸 넣었군" to player
                        exit
                    else:
                        set {_is주문서} to true
                else:
                    set {_is주문서} to false


                if {_강화단계} is not set:
                    message "[무기대장장이]: 먼저 아이템을 강화할 수 있도록 변형을 해야해!" to player
                    exit

                else if {_강화단계} is not {_강화석스탯}:
                    message "[무기대장장이]: 어이! 강화석은 무기보다 1 높은 강화석을 써야한다구!" to player
                    #message "%{_강화단계}%, %{_강화석스탯::1}%"
                    exit

                else if player's money < {_강화단계} * 5000 + 1500 + {_강화단계} * 500:
                    message "[무기대장장이]: 음, 이 무기를 강화하려면 %{_강화단계} * 5000 + 1500 + {_강화단계} * 500%가 필요한데 부족하군."
                    exit

                else:
                    subtract {_강화단계} * 5000 + 1500 + {_강화단계} * 500 from player's money
                    set {_강화석} to slot 30 of current inventory of player
                    set {_am} to amount of {_강화석} in current inventory of player
                    set {_amount} to {_am} - 1
                    set {_am행운석} to amount of {_luckitem} in current inventory of player
                    set {_am주문서} to amount of {_강화주문서} in current inventory of player
                    set {_amount행운석} to {_am행운석} - 1
                    set {_amount주문서} to {_am주문서} - 1

                    delete slot 30 of current inventory of player
                    delete slot 31 of current inventory of player

                    wait 12 tick
                    set slot 30 of current inventory of player to {_amount} of {_강화석}
                    set slot 31 of current inventory of player to {_amount행운석} of {_luckitem}
                    
                    set {_강화확률} to (100 - (8.023 + {_강화단계}) * {_강화단계} * 1.2 + {_강화단계} * 2.53 * 1.5)
                    if {y.%uuid of player%.오피강화보정} is true:
                        set {_강화확률} to 100
                    
                    set {_강화확률} to {_강화확률} + {_행운석스탯}
                    
                    chance of {_강화확률}%:
                        delete slot 20 of current inventory of player
                        set slot 24 of current inventory of player to {_아이템} with lore " " and "&b%{_강화단계} + 1%단계" and "&e==========" and "&a + 체력 %{_강화체력} + 0.5%" and "&c + 공격력 %{_강화공격력} + 1%" and "&9 + 방어력 %{_강화방어력} + 0.8%" and "&e=========="
                        message "[무기대장장이]: 어이! 강화에 성공했어. 잘 쓰길 바래!" to player
                        message "강화확률: %{_강화확률}%"
                        play sound "BLOCK_ANVIL_USE" with volume 1 and pitch 1 at player
                        if {_강화단계} <= 6:
                            launch flickering star colored red and pink at player's location timed 0.8
                        else if {_강화단계} = 7:
                            launch flickering ball large colored red and pink and yellow at player's location timed 0.5
                            wait a second
                            launch flickering ball large colored blue and green and yellow at player's location timed 0.8
                            wait a second
                            launch flickering ball large colored magenta and brown and orange at player's location timed 1
                        exit
                        
                    else:
                        play sound "BLOCK_ANVIL_DESTROY" with volume 1 and pitch 1 at player
                        launch burst colored white at player timed 0.1

                        if {_is행운석} is false:
                            set {_내려갈확률} to 10

                        chance of 5%:
                            if {_is주문서} is true:
                                delete slot 32 of current inventory of player
                                wait 12 tick
                                set slot 32 of current inventory of player to {_amount주문서} of {_강화주문서}
                                message "[무기대장장이]: 파괴 방지 주문서가 아이템을 살렸군. 하마터면 아이템이 없어질뻔했어 허허!" to player
                                exit
                            else:
                                message "[무기대장장이]: 이런, 무기가 파괴되었다... 이거 정말 미안하군" to player
                                delete slot 20 of current inventory of player
                                exit

                        else:
                            chance of {_내려갈확률}%:
                                message "[무기대장장이]: 이런, 강화 단계가 내려갔다..." to player
                                delete slot 20 of current inventory of player
                                set slot 24 of current inventory of player to {_아이템} with lore " " and "&b%{_강화단계} - 1%단계" and "&e==========" and "&a + 체력 %{_강화체력} - 0.5%" and "&c + 공격력 %{_강화공격력} - 1%" and "&9 + 방어력 %{_강화방어력} - 0.8%" and "&e=========="
                                exit
                            else:
                                message "[무기대장장이]: 으음, 강화에 실패했군. 다음엔 성공하길 바래" to player
                                exit
                        
                        
    # 무기 변형 창
    else if iname contains "&2&l무기 변형":
        if click type is left mouse button with shift or right mouse button with shift or right mouse button:
            cancel event
        else:
            if clicked raw slot is not 11 or 13 or 15:
                if clicked raw slot <= 26:
                    cancel event

            else if clicked raw slot is 13:
                cancel event
                if slot 11 of current inventory of player is air:
                    message "[무기대장장이]: 어이! 무기를 올려야지!" to player
                    exit

                if slot 15 of current inventory of player is not air:
                    message "[무기대장장이]: 야! 거긴 아이템이 나오는 곳이야 얼른 빼!"
                    exit

                else:
                    subtract 5000 from player's money
                    set {_w} to slot 11 of current inventory of player
                    delete slot 11 of current inventory of player
                    set slot 15 of current inventory of player to {_w} with lore " " and "&b0단계" and "&e==========" and "&a + 체력 0" and "&c + 공격력 0" and "&9 + 방어력 0" and "&e=========="
                    message "[무기대장장이]: 좋아 무기 변형에 성공했다!" to player
                    play sound "BLOCK_ANVIL_USE" with volume 1 and pitch 1 at player
                    launch ball firework colored yellow at player's location timed 1
                    exit


# 아이템을 넣고 창을 껐을 때
on inventory close:
    if icname contains "&2&l무기 변형":
        set {_무기변형::*} to slot 1 and 7 of current inventory of player
        wait 1 tick
        loop {_무기변형::*}:
            if loop-value is not air:
                set {_bool} to onInvenFull(player)
                if {_bool} is true:
                    drop loop-value at player's location
                else:
                    give loop-value to player
        exit
        

    else if icname contains "&9&l무기 강화":
        set {_무기강화::*} to slot 20 and 24 and 30 and 31 and 32 of current inventory of player
        wait 1 tick
        loop {_무기강화::*}:
            if loop-value is not air:
                set {_bool} to onInvenFull(player)
                if {_bool} is true:
                    drop loop-value at player's location
                else:
                    give loop-value to player
        exit

    
    else if icname contains "&b&l인첸트 강화":
        set {_무기강화::*} to slot 20 and 24 of current inventory of player
        wait 1 tick
        loop {_무기강화::*}:
            if loop-value is not air:
                set {_bool} to onInvenFull(player)
                if {_bool} is true:
                    drop loop-value at player's location
                else:
                    give loop-value to player
        exit




#!==========================================!#
#!                                          !#
#!              커맨드                       !#
#!                                          !#
#!==========================================!#

command /강화 [<string>] [<number>] [<number>]:
    trigger:
        if arg 1 is "확률":
            message "&e=========================="
            loop 8 times:
                set {_강화단계} to loop-value - 1
                set {_강화확률} to (100 - (8.023 + {_강화단계}) * {_강화단계} * 1.2 + {_강화단계} * 2.53 * 1.5)
                message "{@s2} %{_강화단계}% -> %{_강화단계}+1% 단계: %{_강화확률}%%%, 강화비용: %{_강화단계} * 5000 + 1500 + {_강화단계} * 500%" 
            message "&e=========================="
            exit

        else if arg 1 is "강화석":
            if player is not op:
                message "{@s2} 관리자 권한이 필요합니다."
                exit
            else:
                give arg 3 of Nether Quartz named "&6강화석 &d(%arg 2% 단계)" with lore "&b%arg 2 - 1%단계 아이템을 강화하는데 쓰입니다." to player
                message "{@s2} &b%arg 2%단계&r 강화석을 받았습니다."
                exit

        else if arg 1 is "오피":
            if player is not op:
                message "{@s2} 관리자 권한이 필요합니다."
                exit
            else:
                if {y.%uuid of player%.오피강화보정} is not set:
                    set {y.%uuid of player%.오피강화보정} to true
                    message "{@s2} 당신의 강화 확률을 100%%로 만들었습니다."
                    exit
                else:
                    delete {y.%uuid of player%.오피강화보정}
                    message "{@s2} 당신의 강화 확률을 100%% 고정을 취소했습니다."
                    exit

        else if arg 1 is "행운석":
            give 1 of white dye named "&b강화 &6행운석" with lore "&d강화 확률을 &c%arg 2%%% &r올려줍니다." and "&r그리고 강화수치가 &d내려가지 않게 &r해줍니다." and "&d행운석" to player
            message "{@s2} &b강화 &6행운석&r을 받았습니다."
            exit

        else if arg 1 is "주문서":
            give 1 of paper named "&b강화 &2주문서" with lore "&d강화시 &c아이템 파괴&r를 막아줍니다." and "&2주문서" to player
            message "{@s2} &b강화 &2주문서&r를 받았습니다."
            exit

        else:
            give 1 of Glowstone dust named "&9인챈트 &f강화석" with lore "&3%arg 1% &7+ 1" and "&b인챈트" to player
            message "{@s2} &3%arg 1% &9인챈트 &f강화석을 받았습니다."
            exit

command /인챈트 [<enchantment>] [<integer>]:
    trigger:
        set enchantment level of arg 1 of player's tool to arg 2
        message "들고있는 아이템이 &c%arg 1% &3%arg 2%&r(으)로 인챈트 되었습니다."
        exit

#!==========================================!#
#!                                          !#
#!              스탯 설정                    !#
#!                                          !#
#!==========================================!#

every 10 tick:
    set {스탯::1} to "체력"
    set {스탯::2} to "공격력"
    set {스탯::3} to "방어력"

    loop all players:
        # 각 배열 변수에 player 장비의 lore을 대입
        set {_%loop-player%.lore.1::*} to uncolored lore of loop-player's helmet
        set {_%loop-player%.lore.2::*} to uncolored lore of loop-player's chestplate
        set {_%loop-player%.lore.3::*} to uncolored lore of loop-player's Leggings
        set {_%loop-player%.lore.4::*} to uncolored lore of loop-player's boots
        set {_%loop-player%.lore.5::*} to uncolored lore of loop-player's offhand
        set {_%loop-player%.lore.6::*} to uncolored lore of loop-player's tool

        # 각 장비의 갯수 만큼 반복, 그리고 체력, 공격력, 방어력 3번 반복해서 숫자만 남긴 데이터를 변수에 대입
        loop 6 times:
            loop 3 times:
                replace all " " and "+" and {스탯::%loop-value-3%} with "" in {_%loop-player%.lore.%loop-value-2%::%loop-value-3 + 3%}
                set {_%loop-player%.스탯.%loop-value-2%.%{스탯::%loop-value-3%}%} to {_%loop-player%.lore.%loop-value-2%::%loop-value-3 + 3%} parsed as number

        loop 3 times:
            # 플레이어의 체력, 공격력, 방어력 순으로 반복함. 그리고 플레이어의 스탯을 0으로 초기화
            set {%loop-player%.스탯.%{스탯::%loop-value-2%}%} to 0 

            # 6번 반복해서 6개의 장비 스탯을 플레이어의 스탯에 더함.
            loop 6 times:
                add {_%loop-player%.스탯.%loop-value-3%.%{스탯::%loop-value-2%}%} to {%loop-player%.스탯.%{스탯::%loop-value-2%}%}

        set loop-player's max health to {%loop-player%.스탯.%{스탯::1}%}/4 + 10

on damage:
    if attacker is player:
        if {str.%attacker%} is set:
            set damage to damage + {%attacker%.스탯.%{스탯::2}%}/5
            set {test.%attacker%.준데미지} to damage
            #message "데미지: %damage%" to attacker

    if victim is player:
        if {def.%victim%} is set:
            set damage to damage - {%victim%.스탯.%{스탯::3}%}/6
            set {test.%victim%.받은데미지} to damage
            #message "받은 데미지: %damage%" to victim

on join:
    if {test.%player%.준데미지} is not set:
        set {test.%player%.준데미지} to 0
    if {test.%player%.받은데미지} is not set:
        set {test.%player%.받은데미지} to 0

#!==========================================!#
#!                                          !#
#!              함수                        !#
#!                                          !#
#!==========================================!#

# 인벤토리가 꽉찼는지 확인
function onInvenFull(p: player) :: boolean:
    loop all items in the inventory of {_p}:
        add 1 to {_tmp}
    if {_p}'s offhand is not air:
        subtract 1 from {_tmp}
    if {_p}'s boots is not air:
        subtract 1 from {_tmp}
    if {_p}'s leggings is not air:
        subtract 1 from {_tmp}
    if {_p}'s chestplate is not air:
        subtract 1 from {_tmp}
    if {_p}'s helmet is not air:
        subtract 1 from {_tmp}
    if {_tmp} is 36:
        return true
    else:
        return false

# 인챈트 한계치
function isMaxEnchant(s: string, i: number, p: player) :: boolean:
    #message "%{_s}%, %{_i}%" to {_p}
    if {_s} is "수선":
        if {_i} = 2:
            return true
        else:
            return false

    else if {_s} is "차가운 걸음":
        if {_i} = 3:
            return true
        else:
            return false
    
    else if {_s} is "호흡" or "발화" or "빠른 장전" or "충절" or "행운" or "바다의 행운" or "미끼":
        if {_i} = 4:
            return true
        else:
            return false

    else if {_s} is "약탈" or "밀치기" or "휘몰아치는 칼날" or "밀어내기" or "관통" or "급류" or "가시" or "가벼운 착지" or "내구성":
        if {_i} = 5:
            return true
        else:
            return false

    else if {_s} is "보호" or "화염으로부터 보호" or "폭발로부터 보호" or "발사체로부터 보호":
        if {_i} = 6:
            return true
        else:
            return false
        
    else if {_s} is "날카로움" or "강타" or "살충" or "힘" or "찌르기" or "효율":
        if {_i} = 7:
            return true
        else:
            return false

    





# 인챈트 번역
function transEnchant(i: string) :: string:
    # 갑옷
    if {_i} is "보호":
        return "protection"
    else if {_i} is "화염으로부터 보호":
        return "fire protection"
    else if {_i} is "가벼운 착지":
        return "feather falling"
    else if {_i} is "폭발로부터 보호":
        return "blast protection"
    else if {_i} is "발사체로부터 보호":
        return "projectile protection"
    else if {_i} is "호흡":
        return "respiration"
    else if {_i} is "친수성":
        return "aqua affinity"
    else if {_i} is "가시":
        return "thorns"
    else if {_i} is "물갈퀴":
        return "depth strider"
    else if {_i} is "차가운 발걸음":
        return "frost walker"
    # 무기
    else if {_i} is "날카로움":
        return "sharpness"
    else if {_i} is "강타":
        return "smite"
    else if {_i} is "살충":
        return "bane of arthropods"
    else if {_i} is "밀치기":
        return "knockback"
    else if {_i} is "발화":
        return "fire aspect"
    else if {_i} is "약탈":
        return "looting"
    else if {_i} is "휘몰아치는 칼날":
        return "sweeping edge"
    # 도구
    else if {_i} is "효율":
        return "efficiency"
    else if {_i} is "섬세한 손길":
        return "silk touch"
    else if {_i} is "내구성":
        return "unbreaking"
    else if {_i} is "행운":
        return "fortune"
    else if {_i} is "수선":
        return "mending"
    # 활
    else if {_i} is "힘":
        return "power"
    else if {_i} is "밀어내기":
        return "punch"
    else if {_i} is "화염":
        return "flame"
    else if {_i} is "무한":
        return "infinity"
    # 낚싯대
    else if {_i} is "바다의 행운":
        return "luck of the sea"
    else if {_i} is "미끼":
        return "lure"
    # 삼지창
    else if {_i} is "충절":
        return "loyalty"
    else if {_i} is "찌르기":
        return "impaling"
    else if {_i} is "급류":
        return "riptide"
    else if {_i} is "집전":
        return "channeling"
    # 석궁
    else if {_i} is "멀티샷":
        return "multishot"
    else if {_i} is "관통":
        return "piercing"
    else if {_i} is "빠른 장전":
        return "quick charge"
    # 저주 마법
    else if {_i} is "귀속의 저주":
        return "binding curse"
    else if {_i} is "소실의 저주":
        return "vanishing curse"





#!==========================================!#
#!                                          !#
#!              test                        !#
#!                                          !#
#!==========================================!#

on rightclick on entity:
    # 무기 대장장이 오른쪽 클릭
    if entity's name is "test":
        exit

